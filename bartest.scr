#include "ls.icl"
#include "or.icl"

#define VERSION        "1.00"

#cmdbuff 2                             // 2 answer buffers

#define MAINX          450             // Width of main window
#define MAINY          135             // Height of main window

handle           hmain                 // Main window
handle           hcode1                // Data from the first scanner
handle           hcode2                // Data from the second scanner
handle           herror                // Displays error messages
handle           hstart                // Start test
handle           hstop                 // Stop test
handle           hexit                 // Exit button

// If first character is an asterisk (*), there will be no answer.
char             sinit[3,64] = {     \ // Initialization strings
  "*<K200,4>",                       \ // Scan on request
  "*<K201,S>",                       \ // Letter 'A' starts conversion
  "" }                                 // End of sequence

// Sends command and receives answer.
function int Sendcmd(int channel,char *s,int waitanswer)
  int i,t,status
  char buf[1024]
  i=Strlen(s)
  if i==0 return                       // No command
  if i>1000 then
    change herror text="Command is too long"
    return 1
  endif
  buf[0]=S_EXTINIT
  if channel==0 then
    buf[1]=0x02                        // RS-232 on port 2 (reader 1)
  else
    buf[1]=0x03                        // RS-232 on port 3 (reader 2)
  endif
  if waitanswer then
    buf[1]=buf[1] | 0x40               // Answer from device will follow
  endif
  buf[2]=0x1A                          // 9600, even, 1 stop, 7 bit
  buf[3]=(waitanswer?5:2)              // Timeout
  buf[4]=0                             // End of echo
  buf[5]='\n'                          // End of answer
  buf[6]=0                             // End of prompt
  buf[7]=0
  buf[8]=0
  buf[9]=0
  buf[10]=0
  buf[11]=0
  $uint2(buf+12)=i
  Memcpy(buf+14,s,i)
  if SYS.Sendimm(0,i+14,buf)<=0 then
    change herror text="Unable to send command"
    return 1
  endif
  t=Time()
  while $A[0].length==0 do
    if Time()-t>6000 then              // No answer at all from tester
      change herror text="No answer from tester"
      return 1
    endif
    wait
  enddo
  status=$uint2($A[0]+5)
  if status!=0 then
    if status & 0x8000 then
      change herror text="Serial interface absent"
    else if status & 0x0040 then
      change herror text="Answer is too long"
    else if status & 0x0020 then
      change herror text="BREAK detected"
    else if status & 0x0010 then
      change herror text="I/O error while receiving answer"
    else if status & 0x0008 then
      change herror text="Internal error in BARCODE.SCR"
    else if status & 0x0002 then
      change herror text="Timeout while waiting for answer"
    else
      change herror text="I/O error"
    endif
    return 1
  endif
  i=$uint2($A[0]+7)
  if i>0 then
    Memcpy(buf,$A[0]+9,i)
    buf[i]='\0'
    if buf[i-1]=='\r' || buf[i-1]=='\n' buf[i-1]='\0'
    if buf[i-2]=='\r' || buf[i-2]=='\n' buf[i-2]='\0'
    if channel==0 then
      change hcode1 text=buf
    else
      change hcode2 text=buf
    endif
  endif
  if waitanswer==0 delay 50            // Not too fast!..
  return 0
end

function int main()
  int i,port,run,runsel,err
  hmain=control WINDOW
    window=NULL
    position=10,10,MAINX,MAINY
    help=" "
    name=format("条码扫描测试 v%s",VERSION)
    bkcolor=GRAY
    size=1024
  endc
  draw window=hmain font=INFOFONT
  draw at 5,33 text="Code 1:"
  hcode1=control TEXT
    window=hmain
    position=55,13,MAINX-65,23
    help="来自第一个条形码扫描仪的数据"
    font=MEDIUMFONT
    mode=M_BORDER
  endc
  draw at 5,66 text="Code 2:"
  hcode2=control TEXT
    window=hmain
    position=55,46,MAINX-65,23
    help="来自第二个条形码扫描仪的数据"
    font=MEDIUMFONT
    mode=M_BORDER
  endc
  herror=control TEXT
    window=hmain
    position=55,78,MAINX-65,20
    help=" "
    color=LIGHTRED
    font=MEDIUMFONT
  endc
  hstart=control BUTTON
    window=hmain
    position=MAINX-85*3,MAINY-31,75,24
    text="Start"
    help="开始条形码测试"
  endc
  hstop=control BUTTON
    window=hmain
    position=MAINX-85*2,MAINY-31,75,24
    text="停止"
    help="停止条形码测试"
  endc
  disable hstop
  hexit=control BUTTON
    window=hmain
    position=MAINX-85,MAINY-31,75,24
    text="关闭"
    help="关闭窗口"
  endc
  run=0; runsel=-1
  while 1 do
    if Pressed(hexit) then
      break
    else if Pressed(hstart) then
      disable hstart
      enable hstop
      runsel=-1
      clear hcode1
      clear hcode2
      clear herror
      err=0
      for i=0,sinit[i,0]!='\0' && err==0,i++ do
        for port=0,port<2 && err==0,port++ do
          if sinit[i,0]=='*' then      // Don't wait for answer
            err=Sendcmd(port,sinit[i]+1,0)
          else                         // Wait for answer
            err=Sendcmd(port,sinit[i]+1,1)
          endif
        enddo
      enddo
      if err==0 run=1
    else if Pressed(hstop) then
      run=0
    endif
    if run==1 && runsel!=1 then
      disable hstart
      enable hstop
      runsel=run
    else if run==0 && runsel!=0 then
      disable hstop
      enable hstart
      runsel=run
    endif
    if run then
      err=Sendcmd(0,"<S>",1)
      if err==0 err=Sendcmd(1,"<S>",1)
      if err!=0 run=0
    endif
    wait
  enddo
end

